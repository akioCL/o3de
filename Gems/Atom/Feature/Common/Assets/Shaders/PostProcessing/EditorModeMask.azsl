/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#include <Atom/Features/SrgSemantics.azsli>

#include <viewsrg.srgi>
#include <scenesrg.srgi>

ShaderResourceGroup ObjectSrg : SRG_PerObject
{
    //! Id of this draw object for retrieval of transformation matrices.
    uint m_objectId;

    //! Id to fill the mask with for this draw object.
    uint m_maskId;

    //! Returns the matrix for transforming points from Object Space to World Space.
    float4x4 GetWorldMatrix()
    {
        return SceneSrg::GetObjectToWorldMatrix(m_objectId);
    }
}

ShaderResourceGroup PassSrg : SRG_PerPass
{
    //! The non-linear depth buffer data for fragments in the color buffer.
    Texture2D<float> m_depthLinear;

    //! Dimensions of linear depth texture (xy is width and height, zw is 1/width and 1/height).
    float4 m_depthLinearDimensions;
    
    Sampler LinearSampler
    {
        MinFilter = Linear;
        MagFilter = Linear;
        MipFilter = Linear;
        AddressU = Clamp;
        AddressV = Clamp;
        AddressW = Clamp;
    };
}

struct VSInput
{
    // Base fields (required by the template azsli file)...
    float3 m_position : POSITION;
};

struct VSOutput
{
    // Base fields (required by the template azsli file)...
    // "centroid" is needed for SV_Depth to compile
    precise linear centroid float4 m_position : SV_Position;

    // Depth buffer value
    float m_depth : UV0;
};

VSOutput MainVS(VSInput IN)
{
    VSOutput OUT;    
    float3 worldPosition = mul(ObjectSrg::GetWorldMatrix(), float4(IN.m_position, 1.0)).xyz;

    OUT.m_position = mul(ViewSrg::m_viewProjectionMatrix, float4(worldPosition, 1.0));
    OUT.m_position.z *= 1.01;

    // Fragment depth
    float3 ndc = float3(OUT.m_position.x / OUT.m_position.w, OUT.m_position. y/ OUT.m_position.w, OUT.m_position.z / OUT.m_position.w);
    OUT.m_depth = ndc.z;
    return OUT;
}

struct PixelOutput
{
    float4 m_color : SV_Target0;
};

PixelOutput MainPS(VSOutput IN)
{
    PixelOutput OUT;
    // Get Linear Depth
    float2 screenUV = IN.m_position.xy * PassSrg::m_depthLinearDimensions.zw;
    float existingLinearDepth = PassSrg::m_depthLinear.Sample(PassSrg::LinearSampler, screenUV).r;
    float newLinearDepth = IN.m_depth;

    existingLinearDepth = abs(((ViewSrg::GetFarZTimesNearZ()) / (ViewSrg::GetFarZMinusNearZ() * existingLinearDepth - ViewSrg::GetFarZ()))); 
    newLinearDepth = abs(((ViewSrg::GetFarZTimesNearZ()) / (ViewSrg::GetFarZMinusNearZ() * newLinearDepth - ViewSrg::GetFarZ()))); 

    OUT.m_color.g = 1.;
    OUT.m_color.r = 0.0;

    if((newLinearDepth - existingLinearDepth) <= 0.001)
    {
        OUT.m_color.r = 1.0;
    }

    return OUT;
} 