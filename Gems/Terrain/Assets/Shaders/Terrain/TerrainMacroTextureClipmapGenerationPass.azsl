/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */
#include "TerrainMacroTextureClipmapPassSrg.azsli"
#include "ClipmapComputeHelpers.azsli"

#define VIEW_POSITION float3(1024.0, 1024.0, 0.0)

void SampleTexture(uint2 pixelPosition, float2 viewPosition, float2 worldPosition, out float3 macroColor, out float3 macroNormal)
{
    uint2 macroGridResolution = uint2(TerrainSrg::m_macroMaterialGrid.m_resolution >> 16, TerrainSrg::m_macroMaterialGrid.m_resolution & 0xFFFF);
    float macroTileSize = TerrainSrg::m_macroMaterialGrid.m_tileSize;
    float2 macroGridOffset = TerrainSrg::m_macroMaterialGrid.m_offset;
    uint2 macroGridPosition = (worldPosition.xy - macroGridOffset) / macroTileSize;

    uint macroTileIndex = macroGridResolution.x * macroGridPosition.y + macroGridPosition.x;
    static const uint NumMacroMaterialsPerTile = 4;
    macroTileIndex *= NumMacroMaterialsPerTile;


    [unroll] for (uint i = 0; i < NumMacroMaterialsPerTile; ++i)
    {
        TerrainSrg::MacroMaterialData macroMaterialData = TerrainSrg::m_macroMaterialData[macroTileIndex + i];
        if ((macroMaterialData.m_flags & 1) == 0)
        {
            break; // No more macro materials for this tile
        }

        if (any(worldPosition.xy < macroMaterialData.m_boundsMin) || any (worldPosition.xy > macroMaterialData.m_boundsMax))
        {
            continue; // Macro material exists for this tile but is out of the bounds of this particular position
        }

        float2 macroUvSize = macroMaterialData.m_boundsMax - macroMaterialData.m_boundsMin;
        macroUvSize.x = -macroUvSize.x;
        float2 macroUv = (macroMaterialData.m_boundsMin - worldPosition.xy) / macroUvSize;

        // The macro uv gradient can vary massively over the quad because different pixels may choose different macro materials with different UVs.
        // To fix, we use the world position scaled by the macro uv scale which should be fairly uniform across macro materials.
        float2 ddx_macroUv = ddxPosition(pixelPosition, viewPosition.xy, 0) / macroUvSize;
        float2 ddy_macroUv = ddyPosition(pixelPosition, viewPosition.xy, 0) / macroUvSize;

        if (macroMaterialData.m_colorMapId != 0xFFFF)
        {
            macroColor = TerrainSrg::m_textures[macroMaterialData.m_colorMapId].SampleGrad(TerrainMaterialSrg::m_sampler, macroUv, ddx_macroUv, ddy_macroUv).rgb;
            macroColor = TransformColor(macroColor, ColorSpaceId::LinearSRGB, ColorSpaceId::ACEScg);
        }
    }
}

[numthreads(32,32,1)]
void MainCS(
    uint3 dispatchThreadID : SV_DispatchThreadID, 
    uint3 groupID : SV_GroupID, 
    uint groupIndex : SV_GroupIndex)
{
    float3 viewPosition = VIEW_POSITION;
    uint2 pixelPosition = dispatchThreadID.xy;

    [unroll] for (uint clipmapLevel = 0; clipmapLevel < CLIPMAP_STACK_SIZE; ++clipmapLevel)
    {
        float2 worldPosition = CalculateWorldPosition(pixelPosition, viewPosition.xy, clipmapLevel);

        float3 macroColor = TerrainMaterialSrg::m_baseColor.rgb;
        float3 macroNormal = float3(0.0, 0.0, 0.0);

        SampleTexture(pixelPosition, viewPosition.xy, worldPosition, macroColor, macroNormal);

        PassSrg::m_macroColorClipmaps[clipmapLevel][dispatchThreadID.xy].rgba = float4(macroColor, 1.0);
    }

    float2 worldPosition = CalculateWorldPosition(pixelPosition, viewPosition.xy, CLIPMAP_STACK_SIZE);

    float3 macroColor = TerrainMaterialSrg::m_baseColor.rgb;
    float3 macroNormal = float3(0.0, 0.0, 0.0);

    SampleTexture(pixelPosition, viewPosition.xy, worldPosition, macroColor, macroNormal);

    PassSrg::m_macroColorClipmapPyramid[dispatchThreadID.xy].rgba = float4(macroColor, 1.0);
}

